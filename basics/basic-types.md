# Основные типы

D предоставляет ряд основных типов, которые всегда имеют одинаковый размер,
**независимо** от платформы. Единственным исключением является тип `real`,
который предоставляет максимально возможную точность для чисел с плавающей точкой. Также нет разницы в размере целого числа независимо от того, было приложение скомпилировано для 32-битных или 64-битных систем.

<table class="table table-hover">
<tr><td width="250px"><code class="prettyprint">bool</code></td> <td>8 битов</td></tr>
<tr><td><code class="prettyprint">byte, ubyte, char</code></td> <td>8 битов</td></tr>
<tr><td><code class="prettyprint">short, ushort, wchar</code></td> <td>16 битов</td></tr>
<tr><td><code class="prettyprint">int, uint, dchar</code></td> <td>32 бита</td></tr>
<tr><td><code class="prettyprint">long, ulong</code></td> <td>64 бита</td></tr>
</table>

#### Типы с плавающей точкой:

<table class="table table-hover">
<tr><td width="250px"><code class="prettyprint">float</code></td> <td>32 бита</td></tr>
<tr><td><code class="prettyprint">double</code></td> <td>64 бита</td></tr>
<tr><td><code class="prettyprint">real</code></td> <td>Зависит от платформы, 80 битов на 32-битной Intel x86</td></tr>
</table>

Префикс `u` обозначает *unsigned* (беззнаковые) типы. `char` означает UTF-8
символы, `wchar` используется для UTF-16 строк и `dchar` для UTF-32 строк.

Преобразование между переменными разных типов разрешается компилятором только
если не будет потери точности. При этом преобразование между числами с плавающей точкой (например, `double` в `float`) допускается.

Преобразование в другой тип может быть выполнено принудительно с помощью
выражения `cast(<ТИП>) <переменная>`.

Специальное ключевое слово `auto` создаёт переменную и выводит её тип из правой
части выражения. В выражении `auto var = 7` переменной var будет присвоен тип `int`.
Обратите внимание, что тип по-прежнему определяется на этапе компиляции и не может быть изменён, так же как и у переменной с явно указанным типом.

### Свойства типов

У всех типов есть свойство `.init`, которым они инициализируются. Для всех целочисленных типов это `0`, а для типов с плавающей точкой это `NaN` (*not a number*, "не число").

У целочисленных типов и типов с плавающей точкой есть свойство `.max`,
показывающее наибольшее значение, которое может быть представлено этим типом. Целочисленные типы также обладают свойством `.min`, показывающим, какое наименьшее значение может быть представлено. У типов с плавающей точкой вместо этого есть свойство `min_normal`, которое указывает на наименьшее нормальное значение, не равное нулю. У типов с плавающей точкой есть также свойства `.nan` (NaN-значение), `.infinity` (бесконечное значение), `.dig` (количество десятичных знаков, задающих точность), `.mant_dig` (число битов мантиссы) и другие.

У каждого типа есть свойство `.stringof`, с помощью которого можно получить имя типа в виде строки.

### Индексы в D

В D индексы обычно имеют алиас тип `size_t`, так как он достаточно большой, чтобы представить смещение во всей адресуемой памяти - `uint` для 32-битной и
`ulong` для 64-битной архитектур.

### Подробнее

#### Основные ссылки

- [Присваивание](http://ddili.org/ders/d.en/assignment.html)
- [Переменные](http://ddili.org/ders/d.en/variables.html)
- [Арифметические операции](http://ddili.org/ders/d.en/arithmetic.html)
- [Числа с плавающей точкой](http://ddili.org/ders/d.en/floating_point.html)
- [Базовые типы в _Programming in D_](http://ddili.org/ders/d.en/types.html)

#### Дополнительные ссылки

- [Обзор всех базовых типов в D](https://dlang.org/spec/type.html)
- [`auto` и `typeof` в _Programming in D_](http://ddili.org/ders/d.en/auto_and_typeof.html)
- [Свойства типов](https://dlang.org/spec/property.html)

## {SourceCode}

```d
import std.stdio;

void main()
{
    // Большие числа можно разделять
    // символом подчёркивания "_"
    // для улучшения читаемости.
    int b = 7_000_000;
    short c = cast(short) b; // требуется cast
    uint d = b; // хорошо
    int g;
    assert(g == 0);

    auto f = 3.1415f; // .f обозначает тип float

    // typeid(<выражение>) возвращает
    // информацию о типе заданного выражения.
    writeln("type of f is ", typeid(f));
    double pi = f; // хорошо
    // для типов с плавающей точкой
    // допускается неявное понижающее приведение
    float demoted = pi;

    // доступ к свойствам типа
    assert(int.init == 0);
    assert(int.sizeof == 4);
    assert(bool.max == 1);
    writeln(int.min, " ", int.max);
    writeln(int.stringof); // int
}
```
